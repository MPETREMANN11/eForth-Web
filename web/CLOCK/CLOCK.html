<!DOCTYPE html>
<html>
<head>
<title>display animated clock in canvas with eFORTH web</title>
</head>
<body>
<article style="position:absolute;top:0px;">
<h2>try eFORTH web</h2>
<p></p>
<style type='text/css'>
    #ueforth canvas { 
        background-color: #ffffff;
        border: 1px solid #7f7f7f;
    }
</style>

<div id="ueforth"></div>
<script src="../js/ueforth.js"></script>
<script type="text/forth">
web definitions
JSWORD: ellipse { x y rx ry angle div }
  context.ctx.ellipse(x, y, rx, ry, Math.PI * 2 * angle / div, 0, 2 * Math.PI);
~

JSWORD: clearRect { x y width height }
    context.ctx.clearRect(x, y, width, height);
~


\ reset translate and rotate
JSWORD: resetTransform { }
  context.ctx.resetTransform();
~

\ push current hour minutes and seconds on stack
JSWORD: time@ { -- h m s }
    let date = new Date();
    return [date.getHours(), date.getMinutes(), date.getSeconds()];
~

forth definitions

web viewport@ nip value viewMaxHeight
viewMaxHeight constant ctxWidth
viewMaxHeight constant ctxHeight

$0000af constant faceColor
$000000 constant clockHourColor
$5f5f5f constant clockMinColor
$ff0000 constant clockSecColor

ctxHeight 2 / value radius

radius 10 /   value face_width

radius 90 100 */ value clockRadius

\ initialize canvas
web
: initCanvas ( -- )
    resetTransform
    radius radius translate  
    -90 360 rotate
  ;

: drawFace ( -- )
    faceColor color!
    beginPath
    face_width lineWidth
    0 0 clockRadius clockRadius 0 360 ellipse
    stroke
  ;

: drawHrMin { size }
    beginPath
    clockRadius size 100 */ 0 moveTo
    clockRadius 0 lineTo
    stroke
  ;

\ draw hours
: drawHours ( --)
    initCanvas
    face_width 3 5 */ lineWidth
    12 0 do
        1 12 rotate
        85 drawHrMin
    loop
  ;
  
\ draw minutes  
: drawMinutes ( -- )
    face_width 2 12 */ lineWidth
    61 0 do
        1 60 rotate
        90 drawHrMin
    loop
  ;
  
0 value currentHour
0 value currentMinute
0 value currentSecond

: setCurrentTime ( -- )
    time@
    to currentSecond
    to currentMinute
    12 mod to currentHour
  ;
  
: dispHour ( -- )
    initCanvas
    clockHourColor color!
    currentHour 30 *   currentMinute 6 12 */ + 
    360 rotate
    face_width 3 5 */ lineWidth
    beginPath
    clockRadius 5 / negate 0 moveTo
    clockRadius clockRadius 4 / - 0 lineTo
    stroke
  ;

: dispMinute ( -- )
    initCanvas
    clockMinColor color!
    currentMinute 6 * 360 rotate
    face_width 2 5 */ lineWidth
    beginPath
    clockRadius 6 / negate 0 moveTo
    clockRadius clockRadius 5 / - 0 lineTo
    stroke
  ;

: dispSecond ( -- )
    clockSecColor color!
    currentSecond 6 * 
    360 rotate
    1 lineWidth
    beginPath
    clockRadius 6 / negate 0 moveTo
    clockRadius clockRadius 5 / - 0 lineTo
    stroke
  ;
  
: deleteHrMinSec ( -- )
    initCanvas
    $ffffff random color!
    clockRadius 4 5 */ >r
    0 0 r@ r@ -90 360 ellipse rdrop
    fill
  ;

\ draw clock in canvas
: drawClock ( -- )
    gr
    ctxWidth ctxHeight window
    begin
        resetTransform
        0 0 ctxWidth ctxHeight clearRect
        setCurrentTime
        initCanvas
        drawFace
        drawMinutes
        drawHours
        dispHour
        dispMinute
        dispSecond
        1000 ms
        key? if exit then
    again
  ;

drawClock

</script>
<script type="text/forth">    
</script>

</article>
</body>
</html>
